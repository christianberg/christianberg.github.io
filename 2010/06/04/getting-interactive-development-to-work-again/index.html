<!DOCTYPE html>
<html><head><meta content="width=device-width, initial-scale=1" name="viewport"><meta content="Christian Berg" name="author"><link href="//netdna.bootstrapcdn.com/bootstrap/3.1.1/css/bootstrap.min.css" rel="stylesheet"><link href="/atom.xml" rel="alternate" title="Christian Bergs Blog" type="application/atom+xml"><title>Christian Bergs Blog</title></head><body><div class="navbar navbar-default"><div class="container"><div class="navbar-header"><a class="navbar-brand" href="/" title="{:author &quot;Christian Berg&quot;
 :type :blog}">(christianberg.github.io)</a></div><a class="navbar-text navbar-right" href="/archive/">Archive</a></div></div><div class="container"><h1>Getting Interactive Development to Work (Again)</h1><p>In my <a href="/blog/2010/06/01/accessing-the-app-engine-datastore">last post</a> I promised to fix my local development setup to enable the interactive development style typical for Clojure and make the App Engine services (such as the datastore) available from the REPL.</p><p>Others have already tackled the same problem. The best resource I found is the <a href="http://www.hackers-with-attitude.com/2010/04/clojure-google-app-engine-setup-update.html">hackers with attitude blog</a>, another one is <a href="http://carpathia.blogspot.com/2010/05/yet-another-clojure-compojure-google.html">here</a>. My code is largely based on these contributions, I rolled my own version mainly to get a better understanding of the setup.</p>
<!--more--><h2>Initializing the App Engine services</h2><p>To use the App Engine APIs outside of the Google servers, a local ApiProxy and Environment needs to be provided. While the ApiProxy needs to be initialized only once per JVM, the Environment needs to be set for every thread on which API calls are made. This is trivial for the REPL, which runs in one thread. It's a bit more work when you want to run a local Jetty server, since it spawns new threads for handling requests. Fortunately, the design of Ring makes it easy to add so-called middleware to an existing web app that can handle the environment setup.</p><p>Enough said, here's the code. I'll go through it def by def below.</p>
<pre><code class="clojure">(ns local-dev
  &quot;Tools for local development.
   Enables the use of the App Engine APIs on the REPL and in a local Jetty instance.&quot;
  (:use ring.adapter.jetty
        [ring.middleware file file-info])
  (:import [java.io File]
           [java.util HashMap]
           [com.google.apphosting.api ApiProxy ApiProxy$Environment]
           [com.google.appengine.tools.development
            ApiProxyLocalFactory
            LocalServerEnvironment]))

(defonce *server* (atom nil))
(def *port* 8181)

(defn- set-app-engine-environment []
  &quot;Sets up the App Engine environment for the current thread.&quot;
  (let [att (HashMap. {&quot;com.google.appengine.server_url_key&quot;
                       (str &quot;http://localhost:&quot; *port*)})
        env-proxy (proxy [ApiProxy$Environment] []
                    (isLoggedIn [] false)
                    (getRequestNamespace [] &quot;&quot;)
                    (getDefaultNamespace [] &quot;&quot;)
                    (getAttributes [] att)
                    (getAppId [] &quot;_local_&quot;))]
    (ApiProxy/setEnvironmentForCurrentThread env-proxy)))

(defn- set-app-engine-delegate [dir]
  &quot;Initializes the App Engine services. Needs to be run (at least) per JVM.&quot;
  (let [local-env (proxy [LocalServerEnvironment] []
                    (getAppDir [] (File. dir))
                    (getAddress [] &quot;localhost&quot;)
                    (getPort [] *port*)
                    (waitForServerToStart [] nil))
        api-proxy (.create (ApiProxyLocalFactory.)
                           local-env)]
    (ApiProxy/setDelegate api-proxy)))

(defn init-app-engine
  &quot;Initializes the App Engine services and sets up the environment. To be called from the REPL.&quot;
  ([] (init-app-engine &quot;/tmp&quot;))
  ([dir]
     (set-app-engine-delegate dir)
     (set-app-engine-environment)))

(defn wrap-local-app-engine [app]
  &quot;Wraps a ring app to enable the use of App Engine Services.&quot;
  (fn [req]
    (set-app-engine-environment)
    (app req)))

(defn start-server [app]
  &quot;Initializes the App Engine services and (re-)starts a Jetty server
   running the supplied ring app, wrapping it to enable App Engine API use
   and serving of static files.&quot;
  (set-app-engine-delegate &quot;/tmp&quot;)
  (swap! *server* (fn [instance]
                   (when instance
                     (.stop instance))
                   (let [app (-&gt; app
                                 (wrap-local-app-engine)
                                 (wrap-file &quot;./war&quot;)
                                 (wrap-file-info))]
                     (run-jetty app {:port *port*
                                     :join? false})))))

(defn stop-server []
  &quot;Stops the local Jetty server.&quot;
  (swap! *server* #(when % (.stop %))))


</code></pre><p>The code is in a separate namespace, so it doesn't get AOT-compiled and deployed with the rest of the app. I'm using an atom to store the Jetty server instance. Using <code>defonce</code> was helpful while developing this, because I could recompile the file (<code>C-c C-k</code> in Emacs) without losing the reference to the running Jetty server.</p><p>The two functions <code>set-app-engine-environment</code> and <code>set-app-engine-delegate</code> do the necessary setup work on the per-thread and per-jvm basis, respectively.</p><p><code>init-app-engine</code> just calls these two functions. It's intended to be called from the REPL, after which you're able to use API calls like <code>create-entity</code> in the REPL.</p><p><code>wrap-local-app-engine</code> is a Ring middleware that sets the environment for the current thread before passing the request on to the wrapped Ring (or Compojure) app.</p><p>The <code>start-server</code> function takes a Ring app, does the per-JVM setup, wraps the app with the middleware for the per-thread setup and starts a Jetty server running the app. If there already is a Jetty server stored in the atom, it is stopped first, so you can use the function to restart the Jetty as well. The <code>:join? false</code> argument is important, otherwise the call to <code>run-jetty</code> will not return.</p><p>I also added the <code>wrap-file</code> middleware to serve static files (and <code>wrap-file-info</code> to add Content-Type and Content-Length headers). This mimics the behaviour of the Google servers, which by default serve all files included in the war directory. (Note that, unlike the Google servers, this setup also serves the files in the WEB-INF directory. In a production system that would be a security concern, for local development I don't mind.)</p><p>Last (and also least interesting), the <code>stop-server</code> function stops the Jetty server (and sets the atom back to nil).</p><h2>Setting up the classpath</h2><p>To use the local API implementations we need some additional jars on the classpath. Here's the updated project.clj:</p>
<pre><code class="clojure">(defproject compojureongae &quot;0.2.0&quot;
  :description &quot;Example app for deployoing Compojure on Google App Engine&quot;
  :namespaces [compojureongae.core]
  :dependencies [[compojure &quot;0.4.0-RC3&quot;]
                 [ring/ring-servlet &quot;0.2.1&quot;]
                 [hiccup &quot;0.2.4&quot;]
                 [appengine &quot;0.2&quot;]
                 [com.google.appengine/appengine-api-1.0-sdk &quot;1.3.4&quot;]
                 [com.google.appengine/appengine-api-labs &quot;1.3.4&quot;]]
  :dev-dependencies [[swank-clojure &quot;1.2.0&quot;]
                     [ring/ring-jetty-adapter &quot;0.2.0&quot;]
                     [com.google.appengine/appengine-local-runtime &quot;1.3.4&quot;]
                     [com.google.appengine/appengine-api-stubs &quot;1.3.4&quot;]]
  :compile-path &quot;war/WEB-INF/classes&quot;
  :library-path &quot;war/WEB-INF/lib&quot;)
</code></pre><p>The new dependencies go into <code>dev-dependencies</code>, since they mustn't be deployed with the app. However, in the current development version of Leiningen 1.2 (which separates dependencies and dev-dependencies into different directories), the dev-dependencies are apparently only intended for Leiningen plugins such as swank-clojure - they are not put on the classpath for the REPL. I had to patch Leiningen to make this work. Here's the diff:</p>
<pre><code class="diff">diff --git a/src/leiningen/classpath.clj b/src/leiningen/classpath.clj
index 3be7e1f..836740e 100644
--- a/src/leiningen/classpath.clj
+++ b/src/leiningen/classpath.clj
@@ -8,7 +8,9 @@
   &quot;Returns a seq of Files for all the jars in the project&#39;s library directory.&quot;
   [project]
   (filter #(.endsWith (.getName %) &quot;.jar&quot;)
-          (file-seq (file (:library-path project)))))
+          (concat
+           (file-seq (file (:library-path project)))
+           (file-seq (file (str (:root project) &quot;/lib/dev&quot;))))))
 
 (defn make-path
   &quot;Constructs an ant Path object from Files and strings.&quot;

</code></pre><p>I'll try to get this (or something similar) into Leiningen. <strong>Update: I wasn't the only one with this problem. It was recently patched in Leiningen.</strong> Stable Leiningen (1.1) will probably work out of the box - I haven't tried.</p><p>Running <code>lein deps</code> installs the new dependencies. As I said in my last post, you'll likely have to manually install the jars from the App Engine SDK into your local Maven repository. Here's an example command for one of the jars:</p>
<pre><code class="shell">mvn install:install-file -DgroupId=com.google.appengine \
-DartifactId=appengine-api-labs -Dversion=1.3.4 -Dpackaging=jar \
-Dfile=$GAESDK/lib/user/appengine-api-labs-1.3.4.jar
</code></pre><h2>Putting it to use</h2><p>Run <code>lein swank</code>, enter <code>M-x slime-connect</code> in Emacs to connect to the REPL and code away as usual. To call functions that make use of the App Engine API, enter this in the REPL:</p>
<pre><code class="clojure">(require &#39;local-dev)
(local-dev/init-app-engine)
</code></pre><p>To start a Jetty server, just enter:</p>
<pre><code class="clojure">(local-dev/start-server (var example))
</code></pre><p><code>example</code> is the name of the Compojure app defined by <code>defroutes</code> in core.clj. <strong>Update: Using <code>var</code> here allows you to change the definition of <code>example</code> itself without having to restart the server.</strong></p><h2>What's next?</h2><p>The next thing I'm working on is using the Users API for authentication and authorization (instead of the simple security-constraint method described in my <a href="/blog/2010/06/01/accessing-the-app-engine-datastore">last post</a>). I'll need to make some changes to this local-dev code in order to properly test that locally. So stay tuned...</p><p>As always, questions and suggestions are very welcome in the comments section below!</p></div><footer><div class="container"><p class="text-muted"><small>&copy; 2009-2016 Christian Berg</small></p></div></footer><script>
  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

  ga('create', 'UA-21431349-1', 'christianberg.github.io');
  ga('send', 'pageview');
</script></body></html>